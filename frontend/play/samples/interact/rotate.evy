rx := -1
ry := 25

for i := range 300
    theta := i * pi / 90
    rx = 10 + (sin theta) * 7
    ry = 10 + (cos theta) * 7
    draw
    sleep 0.01
end

print "⬆ Touch or mouse here ⬆"

on key k:string
    if k == "ArrowUp"
        ry = clamp ry+1
    else if k == "ArrowDown"
        ry = clamp ry-1
    else if k == "ArrowLeft"
        rx = clamp rx-1
    else if k == "ArrowRight"
        rx = clamp rx+1
    end
    draw
end

on move x:num y:num
    rx = clamp x-2
    ry = clamp y-2
    draw
end

func clamp:num n:num
    return max -6 (min 25 n)
end

func draw
    reset
    clear
    color "red"
    for i := range 12
        theta := i * pi / 6
        x := 50 + (sin theta) * 20
        y := 50 + (cos theta) * 20
        move x y
        circle 1.2
    end

    color "black"
    move rx ry
    rect 2 10
    for i := range 12
        theta := i * pi / 6
        rotate theta // transform
        move rx ry
        rect 2 10
    end
end

func rotate theta:num
    sint := sin theta
    cost := cos theta
    transform cost -sint sint cost 50 50
end

func reset
    transform 1 0 0 1 0 0
end
