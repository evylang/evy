// towers of hanoi

colors := ["red" "orange" "gold" "green" "blue" "darkviolet" "violet" "black"]
towers := [
    []
    [8 7 6 5 4 3 2 1]
    []
]

selected := -1
start_tower := 1
num_pieces := (len towers[start_tower])

// Drawing / positioning
towerx := [17 50 83]
w := 5 // line width
basey := 20

width w
linecap "round"

func draw_towers
    clear "white"
    color "black"
    move 0 basey-(w / 2)
    line 100 basey-(w / 2)
    for tower := range 3
        x := towerx[tower]
        color "black"
        move x basey
        line x basey+42
        for n := range (len towers[tower])
            y := n * w + (w / 2) + basey
            if tower == selected and n == (len towers[tower]) - 1
                y = y + w
            end
            piece := towers[tower][n]
            piece_width := piece * 3 + 5
            color colors[piece - 1]
            move x-(piece_width / 2) y
            line x+(piece_width / 2) y
        end
    end
end

on down x:num _:num
    tower := floor x/100*3
    if selected == -1 and (len towers[tower]) == 0
        print "No pieces on that tower!"
        return
    else if selected == -1
        selected = tower
    else if selected == tower
        selected = -1
    else
        move_piece selected tower
        selected = -1
    end
    draw_towers
end

func move_piece from:num to:num
    if (len towers[to]) > 0 and towers[from][-1] > towers[to][-1]
        print "Piece too big to move there."
        return
    end
    towers[to] = towers[to] + [towers[from][-1]]
    towers[from] = towers[from][:-1]
    if to != start_tower and (len towers[to]) == num_pieces
        print "You win. Have some confetti."
        start_tower = to
    end
end

draw_towers
